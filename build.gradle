/*
 *    Copyright 2017 - 2022 BreadMoirai (Ton Ly)
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

group = 'com.github.breadmoirai'
version = '2.4.1'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation project(':ast')

    implementation gradleApi()

    implementation('com.squareup.okhttp3:okhttp:4.10.0') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
    }
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.0.0'
    implementation 'org.zeroturnaround:zt-exec:1.12'

//    compileOnly 'com.gradle.publish:plugin-publish-plugin:1.0.0-rc-1'

//    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
//        exclude(group: 'org.codehaus.groovy')
//    }
//    testCompile(group: "org.testfx", name: "testfx-core", version: "4.0.13-alpha")
//    testCompile(group: "org.testfx", name: "testfx-spock", version: "4.0.13-alpha")
//    testCompile(gradleTestKit())
}

gradlePlugin {
    website = 'https://github.com/BreadMoirai/github-release-gradle-plugin'
    vcsUrl = 'https://github.com/BreadMoirai/github-release-gradle-plugin'
    plugins.create('github-release') {
        id = 'com.github.breadmoirai.github-release'
        implementationClass = 'com.github.breadmoirai.githubreleaseplugin.GithubReleasePlugin'
        displayName = 'Github Release Plugin'
        description = 'A Gradle Plugin to send Releases to Github '
        tags.set(['github', 'release'])
    }
}

shadowJar.archiveClassifier.set ''

validatePlugins {
    enableStricterValidation = true
}

task createPluginClasspath {
    def outputDir = file("${buildDir}/resources/test")

    inputs.files sourceSets.test.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("${outputDir}/plugin-classpath.txt").text = sourceSets.test.runtimeClasspath.join('\n',)
    }
}
test.dependsOn(createPluginClasspath)

afterEvaluate {
    tasks.named('generateMetadataFileForPluginMavenPublication') {
        dependsOn('jar')
    }
}